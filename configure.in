dnl Process this file with autoconf to produce a configure script.
AC_INIT(portage, 2.1.20, grobian@gentoo.org, portage-prefix)

AC_PREREQ([2.61])

case "${prefix}" in
	'') 	AC_MSG_ERROR([bad value ${prefix} for --prefix, must not be empty]) ;;
	*/)		AC_MSG_ERROR([bad value ${prefix} for --prefix, must not end with '/']) ;;
	/*|NONE) ;;
	*) 		AC_MSG_ERROR([bad value ${prefix} for --prefix, must start with /]) ;;
esac

AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE

dnl Checks for programs.
dnl store clfags prior, otherwise it's not propagated.
if test "x$CFLAGS" != "x"
then
CFLAGS=$CFLAGS
fi

AC_PREFIX_DEFAULT([/usr])

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_EGREP

GENTOO_PATH_XCU_ID()
GENTOO_PATH_PYTHON([2.4])

AC_PATH_PROG(RM, [rm], no)
AC_PATH_PROG(MV, [mv], no)
AC_PATH_PROG(BASENAME, [basename], no)
AC_PATH_PROG(DIRNAME, [dirname], no)
dnl avoid bash internal variable messing up things here
unset BASH
GENTOO_PATH_GNUPROG(BASH, [bash])
GENTOO_PATH_GNUPROG(SED, [sed])
GENTOO_PATH_GNUPROG(WGET, [wget])
GENTOO_PATH_GNUPROG(FIND, [find])
GENTOO_PATH_GNUPROG(XARGS, [xargs])
GENTOO_PATH_GNUPROG(GREP, [grep])

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([string.h strings.h errno.h unistd.h stdio.h stdlib.h])
AC_CHECK_HEADERS([sys/stat.h sys/types.h dirent.h sys/time.h alloca.h])
AC_CHECK_HEADERS([fcntl.h])
AC_CHECK_HEADERS([utime.h])

AC_SEARCH_LIBS([readdir], [c])
AC_SEARCH_LIBS([strcpy], [c])
AC_SEARCH_LIBS([strstr], [c])
AC_SEARCH_LIBS([memcpy], [c])
AC_CHECK_FUNCS([utimes])

AC_MSG_CHECKING([for the *time fields in struct stat])
AC_TRY_LINK([
			 #include <sys/types.h>
			 #include <sys/stat.h>
			 #ifdef HAVE_FCNTL_H
			 #include <fcntl.h>
			 #endif
		 ], [
			 struct stat s;
			 s.st_atimespec.tv_sec = 0;
			 s.st_mtimespec.tv_sec = 0;
		 ], [
			 AC_DEFINE(ATIME_SEC, st_atimespec.tv_sec, [atime seconds for this platform])
			 AC_DEFINE(ATIME_NSEC, st_atimespec.tv_nsec, [atime nanoseconds for this platform])
			 AC_DEFINE(MTIME_SEC, st_mtimespec.tv_sec, [mtime seconds for this platform])
			 AC_DEFINE(MTIME_NSEC, st_mtimespec.tv_nsec, [mtime nanoseconds for this platform])
			 AC_MSG_RESULT([st_*timespec])
		 ], [
AC_TRY_LINK([
			 #include <sys/types.h>
			 #include <sys/stat.h>
			 #ifdef HAVE_FCNTL_H
			 #include <fcntl.h>
			 #endif
		 ], [
			 struct stat s;
			 s.st_atim.tv_sec = 0;
			 s.st_mtim.tv_sec = 0;
		 ], [
			 AC_DEFINE(ATIME_SEC, st_atim.tv_sec, [atime seconds for this platform])
			 AC_DEFINE(ATIME_NSEC, st_atim.tv_nsec, [atime nanoseconds for this platform])
			 AC_DEFINE(MTIME_SEC, st_mtim.tv_sec, [mtime seconds for this platform])
			 AC_DEFINE(MTIME_NSEC, st_mtim.tv_nsec, [mtime nanoseconds for this platform])
			 AC_MSG_RESULT([st_*tim])
		 ], [
AC_TRY_LINK([
			 #include <sys/types.h>
			 #include <sys/stat.h>
			 #ifdef HAVE_FCNTL_H
			 #include <fcntl.h>
			 #endif
		 ], [
			 struct stat s;
			 s.st_atime = 0;
			 s.st_mtime = 0;
		 ], [
			 AC_DEFINE(ATIME_SEC, st_atime, [atime seconds for this platform])
			 AC_DEFINE(MTIME_SEC, st_mtime, [mtime seconds for this platform])
			 AC_MSG_RESULT([st_*time])
		 ], [
			AC_MSG_ERROR([cannot determine])
		 ]
)
		 ]
)
		 ]
)

AC_MSG_CHECKING([if S_ISWHT can be used])
AC_TRY_LINK([
			 #include <sys/types.h>
			 #include <sys/stat.h>
			 #ifdef HAVE_FCNTL_H
			 #include <fcntl.h>
			 #endif
		 ], [
			 struct stat s;
			 if (S_ISWHT(s.st_mode)) {}
		 ], [
			 AC_DEFINE(HAVE_S_ISWHT, 1, [defined when S_ISWHT exists])
			 AC_MSG_RESULT([yes])
		 ], [
			 AC_MSG_RESULT([no])
		 ]
)

AC_MSG_CHECKING([if lchown exists])
AC_TRY_LINK([
			 #include <sys/types.h>
			 #include <unistd.h>
		 ], [
			 lchown("", -1, -1);
		 ], [
			 AC_DEFINE(HAVE_LCHOWN, 1, [defined when LCHOWN exists])
			 AC_MSG_RESULT([yes])
		 ], [
			 AC_MSG_RESULT([no])
		 ]
)

AC_ARG_ENABLE(just-compiled-sources,
AC_HELP_STRING([--enable-just-compiled-sources],[install just the bytecode, not the sources (default no)]),
[case "${enableval}" in
  yes)	enable_py_sources=false;;
  no)	enable_py_sources=true;;
  *)	AC_MSG_ERROR(bad value ${enableval} for --enable-just-compiled-sources);;
esac],
[enable_py_sources=true])

AC_ARG_WITH(portage-user,
AC_HELP_STRING([--with-portage-user=myuser],[use user 'myuser' as portage owner (default portage)]),
[case "${withval}" in
  ""|yes) AC_MSG_ERROR(bad value ${withval} for --with-portage-user);;
  *) portageuser="${withval}";;
esac],
[portageuser="portage"])

AC_ARG_WITH(portage-group,
AC_HELP_STRING([--with-portage-group=mygroup],[use group 'mygroup' as portage users group (default portage)]),
[case "${withval}" in
  ""|yes) AC_MSG_ERROR(bad value ${withval} for --with-portage-group);;
  *) portagegroup="${withval}";;
esac],
[portagegroup="portage"])

AC_ARG_WITH(root-user,
AC_HELP_STRING([--with-root-user=myuser],[uses 'myuser' as owner of installed files (default is portage-user)]),
[case "${withval}" in
  ""|yes) AC_MSG_ERROR(bad value ${withval} for --with-root-user);;
  *) rootuser="${withval}";;
esac],
[rootuser="${portageuser}"])

AC_MSG_CHECKING([for user id of ${rootuser}])
dnl grab uid of rootuser
rootuid=`${XCU_ID} -u "${rootuser}"`
if test "x`echo ${rootuid} | ${EGREP} '^[[0-9]]+$'`" != "x"
then
	AC_MSG_RESULT([${rootuid}])
else
	AC_MSG_ERROR([error finding the user id of ${rootuser}])
fi
AC_MSG_CHECKING([for group id of ${rootuser}])
rootgid=`${XCU_ID} -g "${rootuser}"`
if test "x`echo ${rootgid} | ${EGREP} '^[[0-9]]+$'`" != "x"
then
	AC_MSG_RESULT([${rootgid}])
else
	AC_MSG_ERROR([error finding the group id of ${rootuser}])
fi

AC_ARG_WITH(offset-prefix, 
AC_HELP_STRING([--with-offset-prefix],
			   [specify the installation prefix for all packages, defaults to an empty string]),
			   [DOMAIN_PREFIX=$withval],
			   [DOMAIN_PREFIX=''])

if test "x$DOMAIN_PREFIX" = "x"
then
	PORTAGE_EPREFIX='""'
	EAPIPREFIX='None'
else
	PORTAGE_EPREFIX='path.normpath("'"$DOMAIN_PREFIX"'")'
	EAPIPREFIX='"prefix"'
fi

DEFAULT_PATH="${DOMAIN_PREFIX}/usr/bin:${DOMAIN_PREFIX}/usr/sbin:${DOMAIN_PREFIX}/bin:${DOMAIN_PREFIX}/sbin:${prefix}/bin"
AC_ARG_WITH(default-path, 
AC_HELP_STRING([--with-default-path], [specify additional PATHs available to the portage build environment ]),
[DEFAULT_PATH="${DEFAULT_PATH}":$withval],
[DEFAULT_PATH="${DEFAULT_PATH}"])

AC_CONFIG_FILES([ Makefile ])
AC_CONFIG_FILES([ subst-install ])
AC_CONFIG_FILES([ subst-install.vars.in ])
AC_CONFIG_FILES([ src/Makefile ])
AC_CONFIG_FILES([ man/Makefile ])
AC_CONFIG_FILES([ bin/Makefile ])
AC_CONFIG_FILES([ pym/Makefile ])
AC_CONFIG_FILES([ cnf/Makefile ])

AC_SUBST(portageuser)
AC_SUBST(portagegroup)
AC_SUBST(rootuser)
AC_SUBST(rootuid)
AC_SUBST(rootgid)
AC_SUBST(DOMAIN_PREFIX)
AC_SUBST(PORTAGE_EPREFIX)
AC_SUBST(EAPIPREFIX)
AC_SUBST(DEFAULT_PATH)
AC_SUBST(PORTAGE_BASE,['${exec_prefix}/lib/portage'])
AM_CONDITIONAL(INSTALL_PYTHON_SOURCES, test x$enable_py_sources = xtrue)

AC_MSG_CHECKING([for a list of variables known by configure])
pt_configvars=`sed -n -e "/^ac_subst_vars='/,/'$/p" ${srcdir}/configure | sed -e "s/^ac_subst_vars='//" -e "s/'$//"`
{
	pt_all_configurevars=
	for pt_configvar in ${pt_configvars}
	do
		echo "${pt_configvar}=@${pt_configvar}@"
		pt_all_configurevars="${pt_all_configurevars} ${pt_configvar}"
	done

	echo "all_configurevars='${pt_all_configurevars}'"

} > subst-install.vars.in.in
$as_unset pt_configvar pt_configvars pt_all_configurevars
AC_MSG_RESULT([ok])

AC_OUTPUT
